// No whitespace allowed between digits & variables
number = @{ (ASCII_DIGIT* ~ "."? ~ ASCII_DIGIT+) | (ASCII_DIGIT+ ~ "."? ~ ASCII_DIGIT*) }
variable = @{ ASCII_ALPHA }

unary_minus = { "-" }
factorial = { "!" }

primary = _{ number | variable | "(" ~ expr ~ ")" }
atom = _{ unary_minus? ~ primary ~ factorial? }

bin_op = _{ add | subtract | multiply | divide | power | modulo }
	add = { "+" }
	subtract = { "-" }
	multiply = { "*" }
	divide = { "/" }
    power = { "^" }
	modulo = { "%" }

expr = { atom ~ (bin_op ~ atom)* }

// We can't have SOI and EOI on expr directly, because it is used recursively (e.g. with parentheses)
equation = _{ SOI ~ expr ~ EOI }

WHITESPACE = _{ " " }